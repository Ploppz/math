use builder::{Builder, NodeResult};

grammar<'b>(builder: &'b Builder);

pub Expr: NodeResult<'input> = {
    <l:Expr> "+" <r:Factor> => Ok(builder.add(l?, r?)),
    <l:Expr> "-" <r:Factor> => Ok(builder.sub(l?, r?)),
    Factor,
};

pub Factor: NodeResult<'input> = {
    <l:Num> <r:Pow> => Ok(builder.mul(l?, r?)),
    <l:Factor> "*" <r:Pow> => Ok(builder.mul(l?, r?)),
    <l:Factor> "Â·" <r:Pow> => Ok(builder.mul(l?, r?)),
    <l:Factor> "/" <r:Pow> => builder.div(l?, r?),
    Pow,
};

pub Pow: NodeResult<'input> = {
    <b:Term> "^" <e:Term> => builder.pow(b?, e?),
    <f:r"[[:alpha:]]+"> "(" <g:Expr> ")" => builder.function(f, g?),
    Term,
};

pub Term: NodeResult<'input> = {
    Num,
    Var,
    "(" <Expr> ")",
};

Num: NodeResult<'input> = <s:r"[0-9]+"> => builder.decimal(s);
Var: NodeResult<'input> = <s:r"[[:alpha:]]+"> => Ok(builder.var(s));
